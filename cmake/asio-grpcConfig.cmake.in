# Copyright 2021 Dennis Hezel
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

include("${CMAKE_CURRENT_LIST_DIR}/asio-grpcTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/AsioGrpcProtobufGenerator.cmake")

if(ASIO_GRPC_DISABLE_AUTOLINK)
    return()
endif()

unset(ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES)
unset(ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES)
unset(ASIO_GRPC_UNIFEX_LINK_LIBRARIES)

include(CMakeFindDependencyMacro)

if("@ASIO_GRPC_USE_BOOST_CONTAINER@")
    find_dependency(Boost COMPONENTS container)
    if(TARGET "Boost::container")
        list(APPEND ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES "Boost::container")
        list(APPEND ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES "Boost::container")
        list(APPEND ASIO_GRPC_UNIFEX_LINK_LIBRARIES "Boost::container")
    endif()
else()
    find_package(Boost QUIET)
endif()

if(TARGET "Boost::headers")
    list(APPEND ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES "Boost::headers")
elseif(TARGET "Boost::boost")
    list(APPEND ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES "Boost::boost")
endif()

find_dependency(gRPC)
if(TARGET "gRPC::grpc++")
    list(APPEND ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES "gRPC::grpc++")
    list(APPEND ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES "gRPC::grpc++")
    list(APPEND ASIO_GRPC_UNIFEX_LINK_LIBRARIES "gRPC::grpc++")
endif()

find_package(asio QUIET)
if(TARGET "asio::asio")
    list(APPEND ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES "asio::asio")
endif()

find_package(unifex QUIET)
if(TARGET "unofficial::unifex")
    list(APPEND ASIO_GRPC_UNIFEX_LINK_LIBRARIES "unofficial::unifex")
endif()

if(DEFINED ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES)
    set_target_properties(asio-grpc::asio-grpc PROPERTIES INTERFACE_LINK_LIBRARIES
                                                          "${ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES}")
endif()
if(DEFINED ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES)
    set_target_properties(asio-grpc::asio-grpc-standalone-asio PROPERTIES INTERFACE_LINK_LIBRARIES
                                                                          "${ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES}")
endif()
if(DEFINED ASIO_GRPC_UNIFEX_LINK_LIBRARIES)
    set_target_properties(asio-grpc::asio-grpc-unifex PROPERTIES INTERFACE_LINK_LIBRARIES
                                                                 "${ASIO_GRPC_UNIFEX_LINK_LIBRARIES}")
endif()

unset(ASIO_GRPC_BOOST_ASIO_LINK_LIBRARIES)
unset(ASIO_GRPC_STANDALONE_ASIO_LINK_LIBRARIES)
unset(ASIO_GRPC_UNIFEX_LINK_LIBRARIES)
